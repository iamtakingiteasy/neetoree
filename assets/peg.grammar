           Grammar         <- Spacing Definition+ EndOfFile
           Definition      <- {rule_new} Identifier LEFTARROW {set_spec} Expression {rule_commit}
           Expression      <- {node_new} {group} Sequence {node_commit} ( SLASH {node_new} {group} {set_choice} Sequence {node_commit})*
           Sequence        <- Prefix*
           Prefix          <- {node_new} (AND Action {type_action} / ( AND {pred_and} / NOT {pred_not} )? Suffix) {node_commit}
           Suffix          <- Primary ( QUERY {cardinality_query} / STAR {cardinality_star} / PLUS {cardinality_plus} )?
           Primary         <- Identifier !LEFTARROW {type_ref} {set_spec}
                            / OPEN {group} Expression CLOSE
                            / Literal {type_literal}
                            / Class {type_class}
                            / DOT {type_dot}
                            / Action {type_action}
                            / BEGIN {type_begin}
                            / END {type_end}

           Identifier      <- < IdentStart IdentCont* > Spacing
           IdentStart      <- [a-zA-Z_]
           IdentCont       <- IdentStart / [0-9]
           Literal         <- ['] < ( !['] Char  )* > ['] {set_spec} Spacing
                            / ["] < ( !["] Char  )* > ["] {set_spec} Spacing
           Class           <- '[' < ( !']' Range )* > ']' {set_spec} Spacing
           Range           <- Char '-' Char / Char
           Char            <- '\\' [abefnrtv'"\[\]\\]
                            / '\\' '-'
                            / !'\\' .
           LEFTARROW       <- '<-' Spacing
           SLASH           <- '/' Spacing
           AND             <- '&' Spacing
           NOT             <- '!' Spacing
           QUERY           <- '?' Spacing
           STAR            <- '*' Spacing
           PLUS            <- '+' Spacing
           OPEN            <- '(' Spacing
           CLOSE           <- ')' Spacing
           DOT             <- '.' Spacing
           Spacing         <- ( Space / Comment )*
           Comment         <- '#' ( !EndOfLine . )* EndOfLine
           Space           <- ' ' / '\t' / EndOfLine
           EndOfLine       <- '\r\n' / '\n' / '\r'
           EndOfFile       <- !.
           Action          <- '{' < [^}]* > '}' {set_spec} Spacing
           BEGIN           <- '<' Spacing
           END             <- '>' Spacing
